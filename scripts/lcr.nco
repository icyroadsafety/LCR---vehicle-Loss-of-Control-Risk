/*******************************************************
* calculates lcr 
* renames variables 
* modifies units 
*
* usage ncap2 -4  -A  -S scripts/lcr.nco in.nc out.nc
*******************************************************/

*DBG=1;
*CELSIUSTOFAHRENHEIT=1;
*DO_LCR=1;



// do all calculations in Celsius
// Convert from kelvin to Celsius
// then later to Fahrenheit  
if(exists(tmp2m))
   surfacetemp=tmp2m-273.15f;
 
if(exists(tmpsfc))
   surfacesfc=tmpsfc-273.15f;



if(exists(rh2m)){
    surfacerh=rh2m;
    surfacerh@units="%";
}

// total precipitation
if(exists(apcpsfc))
{
  qpf= apcpsfc; 
  qpf@units="kg/m^2";

 }  


// calculate dew point
if(exists(dpt2m))
{
  // convert from Kelvin to Celsius
  surfacedp=dpt2m-273.15f;
}
else
{
  // calculate dew point using 1980 Bolton formula - (celsius)
  surfacedp=surfacerh/100.0f* 6.112f*exp( (17.67f*surfacetemp) / (surfacetemp+243.5f));
  
 }
  

// calculate wet bulb temperature
// keep it as double for now ?
if(exists(surfacetemp))
{
  
  surfacewb = surfacetemp * atan(0.151977 * (surfacerh + 8.313659)^0.5)
               + atan(surfacetemp + surfacerh)
               - atan(surfacerh - 1.676331)
               + 0.00391838 * (surfacerh)^1.5 * atan(0.023101 * surfacerh)
               - 4.686035;


  
}
  
if(DBG==1){
  surfacedbg=surfacewb.float()-surfacetemp;
  suerface@units="celsius";
  surfacedbg@long_name="** debug wbc  wbc-Temp  [C]";
 }


// Convert from C to F
if(CELSIUSTOFAHRENHEIT==1)
{
   surfacetemp=surfacetemp*1.8f+32.0f;  
   surfacetemp@units="fahrenheit";
   surfacetemp@long_name="** 2 m above ground surface temperature [F] ";

   surfacesfc=surfacesfc*1.8f+32.0f;  
   surfacesfc@units="fahrenheit";
   surfacesfc@long_name="** surface temperature [F] ";

   surfacedp=surfacedp*1.8f+32.0f;  
   surfacedp@units="fahrenheit";
   surfacedp@long_name="** 2 m above ground dew point temperature -  [F]";

   surfacewb=surfacewb*1.8f+32.0f;  
   surfacewb@units="fahrenheit";
   surfacewb@long_name="** 2 m above ground wet bulb  temperature - custom calculation [F]";
  
}
else
{

   surfacetemp@units="celsius";
   surfacetemp@long_name="** 2 m above ground surface temperature [C] ";

   surfacesfc@units="celsius";
   surfacesfc@long_name="** surface temperature [C] ";

   surfacedp@units="celsius";
   surfacedp@long_name="** 2 m above ground dew point temperature -  [C]";

   surfacewb@units="celsius";
   surfacewb@long_name="** 2 m above ground wet bulb  temperature - custom calculation [C]";

 }
   


if(DO_LCR==1)
{  

  lcr[time,lat,lon]=0.0f;

  where  (qpf > 0f && qpf < 0.1f && surfacetemp <= 35f && surfacewb <= 32f)
    lcr = 1;

  where  (qpf>= 0.1f && qpf < 0.25f && surfacetemp <= 35f && surfacewb <= 32f)
    lcr = 2f;

  where (qpf >= 0.25f && surfacetemp <= 35f && surfacewb <= 32f)
    lcr = 3f;
      
  where (lcr != 0.0f && surfacetemp <= 32f)
    lcr = lcr + 1f;
      
  where (lcr != 0f && surfacetemp >= 24f && surfacetemp <= 29f)
    lcr = lcr + 2f;

  where (lcr != 0 && surfacetemp < 32f)
    lcr = lcr + 1f;
      
  where (lcr != 0 && qpf != 0)
    lcr = lcr + 2f;
      
  where (lcr != 0 && lat <= 35)
    lcr = lcr + 3f;
      
  where (qpf == 0f && surfacerh > 99f && surfacetemp < 31f)
    lcr = 3f;
      
  where (qpf == 0f && surfacerh > 98f && surfacerh <= 99f && surfacetemp < 31f)
    lcr = 2f;

  where (qpf == 0f && surfacerh > 97f && surfacerh <= 98f && surfacetemp < 31f)
    lcr = 1f;

  where (qpf == 0f && surfacerh > 97f && surfacetemp < 30f)
    lcr = 2f;

  where (qpf == 0f && surfacerh > 97f && surfacetemp < 28f)
    lcr = 3f;
      
  where (surfacetemp > 36f && surfacewb > 32f && surfacetemp > 30f)
    lcr = 0f;
      
  where (surfacetemp > 38f && surfacewb > 32f)
    lcr = 0f;
      
  where (qpf == 0f && surfacerh <= 97f && surfacetemp > 31f)
    lcr = 0f;

 }

